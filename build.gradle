import groovy.xml.*
import groovy.json.*
import groovy.xml.NamespaceBuilder

Properties props = new Properties()
props.load(new FileInputStream("private.sf.properties"))

ext {
    sfUsername = props.getProperty('sf.username')
    sfPassword = props.getProperty('sf.password')
}

// Ant tasks
// def antTaskPrefix = 'ant-'
// def antGroupName = 'Ant'
// ant.importBuild('build.xml') { 
//     antTaskName -> antTaskPrefix + antTaskName
// }
// tasks.matching { 
//     task -> task.name.startsWith(antTaskPrefix)
// }*.group = antGroupName
// tasks.matching { 
//     task -> task.name.startsWith(antTaskPrefix)
// }.each {
//     task -> task.logging.level = LogLevel.INFO
// }

ant.taskdef(
    resource: 'com/salesforce/antlib.xml',
    uri: 'antlib:com.salesforce',
    classpath: 'ant-salesforce.jar') 
def sfAnt = NamespaceBuilder.newInstance(ant, 'antlib:com.salesforce')

task deployChanges(dependsOn: 'backup') << {
    def gitOutput = getGitOutput();
    logger.info('')
    logger.info('Git Output : ')
    logger.info(gitOutput)
    
    def parseWrapper = parseGitOutput(gitOutput)
    def changedSourceFiles = parseWrapper.changedFiles
    def folderNameChangedFilesMap = parseWrapper.folderFilesMap
    def folderNameDeletedFilesMap = parseWrapper.filderDeletedFilesMap
    
    if (changedSourceFiles.size() == 0) {
        logger.info('')
        logger.quiet('There no changed files for deploy')
        return
    }

    def sourceAndItsMetadataFiles = addMetadataFilesToSources(changedSourceFiles)
    logger.info('')
    logger.info('Changed Files For Deploy : ')
    sourceAndItsMetadataFiles.each {
        file -> logger.info(file)
    }

    delete fileTree(dir: getProjectProperty('build.dir'))
    logger.info('')
    logger.info('Delete Forlder : ' + getProjectProperty('build.dir'))

    copy {
        from getProjectProperty('src.dir')
        into getProjectProperty('build.dir')
        include sourceAndItsMetadataFiles
    }
    logger.info('')
    logger.info('Copy Files From : ' + getProjectProperty('src.dir') + 
        ' To : ' + getProjectProperty('build.dir'))

    def currentSfApiVersion = getCurrentSfApiVersion()
    logger.info('')
    logger.info('Current SF API Version : ' + currentSfApiVersion)

    if (folderNameDeletedFilesMap.keySet().size() > 0) {

        String antOutput = captureAntOutput(ant) {
            tasks.'listMetadata'.execute()
        }

        def folderNameExistDeleteFilesMap = [:]

        folderNameDeletedFilesMap.each { folder, listOfMetadata ->
            listOfMetadata.each { metadataFile ->
                def folderWithMetadata = folder + '/' + metadataFile
                if (antOutput.contains(folderWithMetadata)) {
                    def metadataList = folderNameExistDeleteFilesMap.get(folder)
                    if (metadataList) {
                        metadataList.add(metadataFile)
                    } else{
                        metadataList = [metadataFile]
                    }
                    folderNameExistDeleteFilesMap.put(folder, metadataList)
                }
            }
        }

        if (folderNameExistDeleteFilesMap.keySet().size() > 0) {
            def destructiveChangesXmlString = createPackageXml(folderNameExistDeleteFilesMap)
            logger.info('')
            logger.info('destructiveChnges.xml : \r\n' + destructiveChangesXmlString)

            def destructiveChangesXml = 
                new File(getProjectProperty('build.dir') + '/src/destructiveChanges.xml')
            destructiveChangesXml.text = destructiveChangesXmlString
        }
    }

    def packageXmlString = createPackageXml(folderNameChangedFilesMap)
    logger.info('')
    logger.info('Package.xml : \r\n' + packageXmlString)

    def packageXml = new File(getProjectProperty('build.dir') + '/src/package.xml')
    packageXml.text = packageXmlString

    tasks.'deployMetadata'.execute()
}

task backup << {
    def backupDir = getProjectProperty('backup.dir')

    def folder = new File(backupDir)
    if (!folder.exists()) {
        folder.mkdirs()
    } else {
        folder.deleteDir()
    }

    def result = exec {
        executable = 'git'
        args = [
            'clone', 
            getProjectProperty('git.repository.url'),
            backupDir
        ]
    }

    String gitOutput = '';
    new ByteArrayOutputStream().withStream { os ->
         result = exec {
            workingDir = backupDir
            executable = 'git'
            args = [
                'branch', 
                '-r'
            ]
            standardOutput = os
        }
        gitOutput = os.toString()
    }

    def remote = getProjectProperty('git.remote.name')
    def backupBranch = getProjectProperty('git.backup.branch')
    if (!gitOutput.contains(remote + '/' + backupBranch)) {
        result = exec {
            workingDir = backupDir
            executable = 'git'
            args = [
                'checkout', 
                '-b',
                backupBranch
            ]
        }
    } else {
        result = exec {
            workingDir = backupDir
            executable = 'git'
            args = [
                'checkout', 
                backupBranch
            ]
        }
    }

    tasks.'retrieveMetadata'.execute()

    result = exec {
        workingDir = backupDir
        executable = 'git'
        args = [
            'add', 
            '-A'
        ]
    }

    gitOutput = '';
    new ByteArrayOutputStream().withStream { os ->
         result = exec {
            workingDir = backupDir
            executable = 'git'
            args = [
                'diff', 
                'HEAD', 
                '--name-only'
            ]
            standardOutput = os
        }
        gitOutput = os.toString()
    }

    Boolean isAnyChanges = gitOutput != null && gitOutput.trim() != '';
    if (!isAnyChanges) {
        logger.quiet('')
        logger.quiet('There are no chages. Backup is up to date.')
        return
    }

    result = exec {
        executable = 'git'
        args = [
            'commit', 
            '-m',
            getProjectProperty('git.commit.backup.msg') + ' ' + new Date()
        ]
        workingDir = backupDir
    }

    result = exec {
        executable = 'git'
        args = [
            'push', 
            remote,
            backupBranch
        ]
        workingDir = backupDir
    }
}

def String getGitOutput() {
    String gitOutput = '';
    new ByteArrayOutputStream().withStream { os ->
        def result = exec {
            executable = 'git'
            args = [
                'diff-tree', 
                '--no-commit-id', 
                '--name-status', 
                '-r', 
                getProjectProperty('git.commit.id.last'),
                getProjectProperty('git.commit.id.first') 
            ]
            standardOutput = os
        }
        gitOutput = os.toString()
    }
    return gitOutput;
}

def Expando parseGitOutput(String output) {
    def changedFiles = output.split('\\n')

    def changedSourceFiles = [];
    def folderNameChangedFilesMap = [:]
    def folderNameDeletedFilesMap = [:]

    changedFiles.each { changedFile ->

        // Possible Modifiers
        // ' ' = unmodified
        // M = modified
        // A = added
        // D = deleted
        // R = renamed
        // C = copied
        // U = updated but unmerged
        def modifier = changedFile.substring(0, 1)

        // Remove Modifier
        changedFile = changedFile.substring(1, changedFile.length()).trim()

        def filePath = changedFile.split('/')
        if (filePath.size() >= 3) {
            def metadataFolderName = filePath[1]

            def changedFilesInFolder = null
            if (modifier == 'D') {
                changedFilesInFolder = folderNameDeletedFilesMap.get(metadataFolderName)
            } else {
                changedFilesInFolder = folderNameChangedFilesMap.get(metadataFolderName)
            }

            def currentFile = filePath[filePath.size() - 1]
            if (currentFile.indexOf('-meta.xml') == -1) {
                def currentFileWithoutExtension = currentFile
                    .substring(0, currentFile.indexOf('.'))
                if (changedFilesInFolder) {
                    changedFilesInFolder.add(currentFileWithoutExtension)
                } else {
                    changedFilesInFolder = [ currentFileWithoutExtension ]
                }
                if (modifier == 'D') {
                    folderNameDeletedFilesMap.put(metadataFolderName, changedFilesInFolder)
                } else {
                    changedSourceFiles.add(changedFile)
                    folderNameChangedFilesMap.put(metadataFolderName, changedFilesInFolder)
                }
            }
        }
    }
    return new Expando( changedFiles : changedSourceFiles,
        folderFilesMap : folderNameChangedFilesMap,
        filderDeletedFilesMap : folderNameDeletedFilesMap);
}

def List<String> addMetadataFilesToSources(List<String> changedSourceFiles) {
    def sourceAndItsMetadataFiles = []
    for (def i in changedSourceFiles) {
        sourceAndItsMetadataFiles.add(i)
        sourceAndItsMetadataFiles.add(i + '-meta.xml')
    }
    return sourceAndItsMetadataFiles
}

def String getCurrentSfApiVersion() {
    def originalPackageXml = new XmlParser()
        .parse('src/package.xml')
    return originalPackageXml.get('version').text();
}

def String createPackageXml(Map<String, List<String>> folderNameChangedFilesMap) {
    def metadataDescribe = new JsonSlurper()
        .parse(file(getProjectProperty('metadata.describe.location')))

    def stringWriter = new StringWriter()
    def builder = new MarkupBuilder(stringWriter)
    builder.mkp.xmlDeclaration(version: "1.0", encoding: "UTF-8")
    builder.package('xmlns' : "http://soap.sforce.com/2006/04/metadata"){
        for(filderName in folderNameChangedFilesMap.keySet()) {
            types() {
                def files = folderNameChangedFilesMap.get(filderName)
                for(file in files) {
                    members(file)
                }
                def theNode = metadataDescribe.metadataObjects
                    .find { it.directoryName == filderName }
                delegate.name(theNode.xmlName)
            }
        }
        delegate.version(currentSfApiVersion)
    }
    return stringWriter.toString()
}

def String getProjectProperty(String propertyName) {
    String customProprty = null
    if (hasProperty(propertyName)) {
        customProprty = this.properties[propertyName]
    }
    return customProprty
}

def captureAntOutput(ant, Closure command) {
    def buffer = new ByteArrayOutputStream()
    def captureStream = new PrintStream(buffer, true, "UTF-8")
    def listener = new org.apache.tools.ant.DefaultLogger(
        errorPrintStream: captureStream,
        outputPrintStream: captureStream,
        messageOutputLevel: org.apache.tools.ant.Project.MSG_INFO
    )

    ant.project.addBuildListener(listener)
    project.configure(ant, command)
    ant.project.removeBuildListener(listener)

    return buffer.toString("UTF-8");
}

// Standard task

task retrieveMetadata << {
    sfAnt.retrieve(
        username : sfUsername, 
        password : sfPassword, 
        serverurl : getProjectProperty('sf.serverurl'), 
        maxPoll : getProjectProperty('sf.maxPoll'), 
        retrieveTarget : getProjectProperty('sf.path.retrieveTarget'), 
        unpackaged : getProjectProperty('sf.path.packageXml')
    )
}

task deployMetadata << {
    sfAnt.deploy(
        serverurl : getProjectProperty('sf.serverurl'), 
        username : sfUsername,
        password : sfPassword,
        maxPoll : getProjectProperty('sf.maxPoll'), 
        deployRoot : getProjectProperty('sf.deployRoot'), 
        rollbackOnError : getProjectProperty('sf.rollbackOnError'), 
        checkOnly : getProjectProperty('sf.checkOnly'),
        runAllTests : getProjectProperty('sf.runAllTests')
    )
}
// <!-- logType="Debugonly" -->
// <!-- <runTest>SampleDeployClass</runTest> -->

task listMetadata << {
    sfAnt.listMetadata(
        username : sfUsername, 
        password : sfPassword, 
        serverurl : getProjectProperty('sf.serverurl'), 
        metadataType : getProjectProperty('sf.metadataType')
    )
}

task bulkRetrieve << {
    sfAnt.bulkRetrieve( 
        username : sfUsername, 
        password : sfPassword, 
        serverurl : getProjectProperty('sf.serverurl'), 
        maxPoll : getProjectProperty('sf.maxPoll'), 
        metadataType : getProjectProperty('sf.metadataType'), 
        retrieveTarget : getProjectProperty('sf.metadataDir')
    )
}

task describeMetadata << {
    sfAnt.describeMetadata(
        username : sfUsername, 
        password : sfPassword, 
        serverurl : getProjectProperty('sf.serverurl'),
        resultFilePath : 'describe.log'
    )
}


tasks.each {
    task -> task.group = 'gres.sf'
}